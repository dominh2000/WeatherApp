plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'dagger.hilt.android.plugin'
    id 'com.google.firebase.crashlytics'
    id 'com.google.devtools.ksp'
}

def properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
}

android {
    signingConfigs {
        release {
            storeFile file('E:\\AndroidStudioProjects\\WeatherKotlin\\keystore\\release.jks')
            storePassword 'Hello123!'
            keyAlias 'upload'
            keyPassword 'Hello123!'
        }
    }
    namespace 'com.example.weatherkotlin'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.weatherkotlin"
        minSdk 21
        targetSdk 34
        versionCode 11
        versionName "1.1.13"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental": "true"
                ]
            }
        }

        resValue "string", "sqlite_password", properties.getProperty("sqlite.password", "")
        resValue "string", "open_weather_api_key", properties.getProperty("openweather.api.key", "")
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_18
        targetCompatibility JavaVersion.VERSION_18
    }
    kotlinOptions {
        jvmTarget = '18'
    }
    kotlin {
        jvmToolchain {
            languageVersion = JavaLanguageVersion.of(20)
            vendor = JvmVendorSpec.ORACLE
        }
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {

    implementation "androidx.activity:activity-ktx:$activity_ver"
    implementation "androidx.core:core-ktx:$core_ktx_ver"
    implementation "androidx.appcompat:appcompat:$appcompat_ver"
    implementation "com.google.android.material:material:$material_ver"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_ver"
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"

    // ViewModel LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_ver"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_ver"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_ver"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_ver"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_kotlin_ver"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_ver"
    // Retrofit with Moshi Converter
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_ver"

    // Coil with SVG
    implementation "io.coil-kt:coil:$coil_ver"
    implementation "io.coil-kt:coil-svg:$coil_ver"

    // Room
    implementation "androidx.room:room-runtime:$room_ver"
    ksp "androidx.room:room-compiler:$room_ver"
    implementation "androidx.room:room-ktx:$room_ver"

    // SQLCipher
    implementation "net.zetetic:android-database-sqlcipher:$sqlcipher_ver"

    // WorkManager + Hilt
    implementation "androidx.work:work-runtime-ktx:$work_ver"
    implementation "androidx.hilt:hilt-work:$hilt_work_ver"
    ksp "androidx.hilt:hilt-compiler:$hilt_compiler_ver"

    // Firebase BOM
    // Crashlytics, Google Play Services, Authentication UI + Support Android 12+
    implementation platform("com.google.firebase:firebase-bom:$firebase_bom_ver")
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.firebaseui:firebase-ui-auth:$auth_ui_ver"
    implementation "com.google.android.gms:play-services-auth:$play_auth_ver"
    implementation "com.google.android.gms:play-services-location:$play_location_ver"

    // Preferences DataStore
    implementation "androidx.datastore:datastore-preferences:$datastore_ver"

    // Paris for adding View style dynamically
    implementation "com.airbnb.android:paris:$paris_ver"

    // Dagger-Hilt
    implementation "com.google.dagger:hilt-android:$hilt_ver"
    ksp "com.google.dagger:hilt-compiler:$hilt_ver"

    // Chucker
    debugImplementation "com.github.chuckerteam.chucker:library:$chucker_ver"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:$chucker_ver"

    // LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_ver"
}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}