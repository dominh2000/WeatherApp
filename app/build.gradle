plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'dagger.hilt.android.plugin'
    id 'com.google.firebase.crashlytics'
    id 'com.google.devtools.ksp'
}

def properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
}

android {
    signingConfigs {
        release {
            storeFile file('E:\\AndroidStudioProjects\\WeatherKotlin\\keystore\\release.jks')
            storePassword 'Hello123!'
            keyAlias 'upload'
            keyPassword 'Hello123!'
        }
    }
    namespace 'com.example.weatherkotlin'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.weatherkotlin"
        minSdk 21
        targetSdk 33
        versionCode 9
        versionName "1.1.11"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental": "true"
                ]
            }
        }

        resValue "string", "sqlite_password", properties.getProperty("sqlite.password", "")
        resValue "string", "open_weather_api_key", properties.getProperty("openweather.api.key", "")
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_18
        targetCompatibility JavaVersion.VERSION_18
    }
    kotlinOptions {
        jvmTarget = '18'
    }
    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of("18"))
        }
    }
    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
}

dependencies {

    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.9.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"

    // ViewModel LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.6.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.6.0"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.15.0"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    // Retrofit with Moshi Converter
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    // Coil with SVG
    implementation "io.coil-kt:coil:2.4.0"
    implementation "io.coil-kt:coil-svg:2.4.0"

    // Room
    implementation "androidx.room:room-runtime:2.5.1"
    ksp "androidx.room:room-compiler:2.5.1"
    implementation "androidx.room:room-ktx:2.5.1"

    // SQLCipher
    implementation 'net.zetetic:android-database-sqlcipher:4.5.4'

    // WorkManager + Hilt
    implementation "androidx.work:work-runtime-ktx:2.8.1"
    implementation "androidx.hilt:hilt-work:1.0.0"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    // Firebase BOM
    // Crashlytics, Google Play Services, Authentication UI + Support Android 12+
    implementation platform('com.google.firebase:firebase-bom:32.1.0')
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation 'com.firebaseui:firebase-ui-auth:8.0.2'
    implementation "com.google.android.gms:play-services-auth:20.5.0"
    implementation "com.google.android.gms:play-services-location:21.0.1"

    // Preferences DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Paris for adding View style dynamically
    implementation 'com.airbnb.android:paris:2.0.2'

    // Dagger-Hilt
    implementation "com.google.dagger:hilt-android:2.46.1"
    kapt "com.google.dagger:hilt-compiler:2.46.1"

    // Chucker
    debugImplementation "com.github.chuckerteam.chucker:library:3.5.2"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:3.5.2"

    // LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.11"
}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}